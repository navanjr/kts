create procedure dbo.glPost (@id int, @styp char(1), @smode int = 0) as 
begin

 declare
  @slink varchar(15) = @styp + cast(@id as varchar),
  @code int,
  @message varchar(500),
  @fpId int,
  @postDate int

 -- first we look to see if this is good to post
 exec dbo.glCheck @id, @styp, @code output, @message output, @fpId output, @postDate output
 if @code = 1
 begin
  select '@code=' + cast(@code as varchar) + ';@message=' + @message + ';@fpid=' + cast(@fpId as varchar) + ';'
  return
 end

 if @code = 0
 begin

  declare 
--   @fpCode varchar(50),
--   @fpDesc varchar(50),
   @balance money,
   @rowCount int,
   @absValue money,
   @rowCount2 int,
   @absValue2 money,
   @sql varchar(500),
   @result int,
   @idToken int,
   @slinkToken varchar(15)

/*
  -- get fiscal code and description from the Fiscal Calendar
  select
   @fpCode = key1, 
   @fpDesc = key2 
  from object where typ = 4700 and id = @fpid
*/

  declare @slinks table(id int identity(1,1), slink varchar(15), processFlag int)
  insert @slinks select slink, 0 from dbo.glGetAllSlinks(@slink)

  -- get the stats to do the final compare
  -- TODO: we can bail here if we are missing anything, balance not 0, no stage detail, etc....
  select
   @balance = sum(amount),
   @rowCount = count(*),
   @absValue = sum(abs(amount)) 
  from glDetailStage 
  where slink in (select slink from @slinks)

  begin transaction
   
   while exists(select * from @slinks where processFlag = 0)
   begin
    select top 1 @idToken = id, @slinkToken = slink from @slinks where processFlag = 0
    exec dbo.glPostGuts @slinkToken, @result output
    update @slinks set processFlag = 1 where id = @idToken
   end

   -- check to see if the post was successful
   select
    @rowCount2 = count(*),
    @absValue2 = sum(abs(amount)) 
   from glDetail 
   where slink in (select slink from @slinks)
   
   if @rowCount = @rowCount2 and @absValue = @absValue2 and @absValue > 0
   begin
    set @message = 'Posting complete.'
    commit transaction
   end
   else
   begin 
    if @smode=0
     begin
      select @code = 1, @message = 'Posting transaction was rolled back.  oops.  please contact support.'
     end
    rollback transaction
   end
  if @smode=0
   begin
    select '@code=' + cast(@code as varchar) + ';@message=' + @message + ';@fpid=' + cast(@fpId as varchar) + ';'
   end
  return
 end

 return
end













