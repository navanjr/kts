create function dbo.receiptCheck(@sourceId int, @paycode varchar(50) = null) returns
@rettable table(
 code int,
 message varchar(500),
 receiptType varchar(50),
 suspenseAccountId int, 
 contraId int,
 arAccountId int,
 fpid int,
 status varchar(50),
 objectTyp int,
 balanceDue money,
 exempt varchar(10)
)
begin
 
 declare
  @cdate int,
  @receiptType varchar(50),
  @suspenseAccountId int,
  @contraId int,
  @contraAccountCode varchar(50),
  @arAccountId int,
  @fpid int,
  @status varchar(50),
  @objectTyp int,
  @officialfund varchar(50),
  @balanceDue money,
  @exempt varchar(10)

 select
  @cdate = dbo.depositDate(key3),
  @receiptType = key3,
  @status = a17,
  @objectTyp = typ,
  @officialfund = a5,
  @exempt = case when olink3 = '1' then 'TRUE' else 'FALSE' end  
 from object with (NOLOCK) where id = @sourceId

 select @balanceDue = dbo.receiptBalanceDue(@sourceId)

 -- check for status of Posted
 if @status = 'Posted'
 begin
  insert @rettable 
   select 1,'Receipt has already been posted.',@receiptType,@suspenseAccountId,@contraId,@arAccountId,@fpid,@status,@objectTyp,@balanceDue,@exempt
  return
 end

 -- check for open Fiscal Period
 select @fpid = dbo.glGetFiscalPeriodId(@cdate) 
 if @fpid = 0
 begin
  insert @rettable 
   select 1,'Fiscal Period is locked or does not exist.',@receiptType,@suspenseAccountId,@contraId,@arAccountId,@fpid,@status,@objectTyp,@balanceDue,@exempt
  return
 end

 -- check for contra account id if we are not a tax receipt
 if @receiptType != 'TAX'
 begin
  if @receiptType = 'OFFICIAL'
   begin
    select @contraId = accountId from glaccounts where accountCode = @officialfund
    select @contraAccountCode = key3 from object with (NOLOCK) where typ=4503 and key1 = 'OFFICIAL'
   end
  else
   begin
    select @contraId = link2, @contraAccountCode = key3 from object with (NOLOCK) where typ=4503 and key1 = @receiptType
   end
 
  if isnull(@contraId,0) = 0
  begin
   insert @rettable
    select 1,'Missing ' + @receiptType + ' Account Information. Contact Support.', @receiptType, @suspenseAccountId, @contraId, @arAccountId, @fpid, @status,@objectTyp,@balanceDue,@exempt
   return
  end

  -- check for AR account
  select @arAccountId = accountId from glAccounts where accountType = 'RECEIVABLE' and targetAccountCode = @contraAccountCode 
  if isnull(@arAccountId,0) = 0
  begin
   insert @rettable
    select 1,'Missing AR Account Information. Contact Support.',@receiptType,@suspenseAccountId,@contraId,@arAccountId,@fpid,@status,@objectTyp,@balanceDue,@exempt
   return
  end
 end

 -- optionally check for the suspense account id
 if isnull(@paycode,'') > '  0'
 begin
  select @suspenseAccountId = link1 from object with (NOLOCK) where typ = 4505 and key1 = @paycode 
  
  if not isnull((select id from object with (NOLOCK) where typ=4701 and id=@suspenseAccountId),0)>0  /*isnull(@suspenseAccountId,0) > 0*/
  begin
   insert @rettable 
    select 1,'Missing G/L Suspense Account Information. Contact Support.',@receiptType,@suspenseAccountId,@contraId,@arAccountId,@fpid,@status,@objectTyp,@balanceDue,@exempt
   return 
  end
 end
 
 insert @rettable
  select 0,'OK',@receiptType,@suspenseAccountId,@contraId,@arAccountId,@fpid,@status,@objectTyp,@balanceDue,@exempt
 return

end