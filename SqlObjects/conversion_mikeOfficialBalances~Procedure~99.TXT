create proc dbo.conversion_mikeOfficialBalances(
 @mode varchar(50) = 'conversion',
 @method varchar(5) = 'GET',
 @class varchar(50) = null output,
 @code int = 0 output,
 @message varchar(100) = null output,
 @tally int = 0 output,
 @fixProc varchar(100) = null output,
 @guide varchar(100) = null output
) as 
begin
 set nocount on

 if @mode != 'conversion'
  return

 declare
  @officialbankcode varchar(50) = dbo.settingsF('conversion.officialbankcode',''),
  @fy varchar(50),
  @mikeTableName varchar(50) = 'balance',
  @mikePath varchar(max),
  @sql nvarchar(max),
  @monthColumnName varchar(3)
 
 declare @st table(
  dptno varchar(50),
  bbal money,
  csv varchar(max)
 )
 
 select @fy = RIGHT(fiscalYear,2) from dbo.fpInfo(dbo.clarionToday())
 select @monthColumnName = left(datename(month,dateadd(month,datepart(m,cast(dbo.settingsF('conversion.conversionDate',null) as datetime)) - 1,'1900-01-01')),3)

 select
  @mikePath = dbo.settingsF('conversion.mikepath','') + '\' + @mikeTableName + '.dbf',
  @sql = 'select ''OFF'' + dptno,'+@monthColumnName+' * -1,null from openrowset(''VFPOLEDB'',''' + @mikePath + ''';'''';'''',''SELECT * FROM ' + @mikeTableName + ' where fyr = "'+@fy+'" and dptno > ""'')'

 insert @st exec(@sql)

 insert @st select upper(@officialBankCode), sum(abs(bbal)), null from @st

 update @st set csv = dptno + ',' + CAST(bbal as varchar) + ',' 


 if @method = 'GET' 
 begin  
   select 
    @class = 'conversion',
    @guide = 'GET|SHOW|FIX',
    @message = 'official Beginning Balances not found in KTS'

   if not exists(select * from object where typ = 4512 and key3 = 'OFFICIAL BEGINNING BALANCES' and olink5 = 'mike')
    select 
     @code = case when count(*) > 0 then 1 else 0 end,
     @tally = COUNT(*)
    from @st
   else
    select 
     @code = 0 ,
     @tally = 0  

 end

 if @method = 'SHOW'
 begin  

  select * from @st order by dptno

 end

 if @method = 'FIX'
 begin
  declare
   @conversionDateClarionDate int = dbo.clarionDate(dbo.settingsF('conversion.conversionDate','')),
   @resultCode int,
   @resultMessage varchar(max),
   @newId int,
   @slink varchar(15),
   @csvBlob varchar(max) = ''

  select @csvBlob = @csvBlob + csv + char(13) + char(10) from @st

-- create J/E with "adjust to" stage and post
  exec dbo.journalEntryCRUD
   @mode = 0,
   @description = 'OFFICIAL BEGINNING BALANCES',
   @jeType = 'MANUAL',
   @postDate = @conversionDateClarionDate,
   @conversionNote = 'mike',
   @resultCode = @resultCode output,
   @resultMessage = @resultMessage output,
   @newId = @newId output

  select @slink = 'o' + cast(@newId as varchar)

-- create stage 
  exec dbo.journalEntryDetailImport @newId, 'TRUE', @csvBlob, @verbose = 'FALSE'

-- post to the gl
  exec dbo.glPost @newId, 'o'
  exec dbo.journalEntryUpdate @newId

  return

 end

end