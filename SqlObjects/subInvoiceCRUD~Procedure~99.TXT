CREATE procedure [dbo].[subInvoiceCRUD](
 @mainInvoiceId int,
 @invoiceDate varchar(10),
 @description varchar(60),
 @sourceCode varchar(50),
 @fundCode varchar(50),
 @subInvoiceType varchar(1) = 'F',
 @amount money,
 @smode int = 0,
 @origin varchar(50) = '',
 @originId int = 0
) as
begin

 exec dbo.logit @@procid, '@mainInvoiceId', @mainInvoiceId, '@invoiceDate', @invoiceDate,'@amount', @amount

 declare 
  @invoiceId int,
  @styp char(1) = 't',
  @slink varchar(15),
  @fpid int,
  @targetcode varchar(50) = '',
  @accrualcode varchar(50) = '',
  @accountBlob varchar(1000),
  @targetAmount money


 declare @rd table(description varchar(60),
   subDescription varchar(60),
   sourceCode varchar(60),
   fundCode varchar(60), 
   amount money)


 -- check for open Fiscal Period
 select @fpid = dbo.glGetFiscalPeriodId(@invoiceDate) 
 if @fpid = 0
 begin
  select '@code=1; Fiscal Period is locked or does not exist.'
  return
 end


 if @subInvoiceType = 'A'
 begin
  if @description like 'ADJUST TO:%'
  begin
   select @targetAmount=@amount, @description = substring(@description+replicate(' ',50),11,50)
   select @amount = @targetAmount-sum(amount) from dbo.invoiceTotalTF(@mainInvoiceId) where accountType = 'RECEIVABLE'
  end

  if left(@description,4) = 'VOID'
  begin
   insert @rd
    select @description,
           left(rtrim(sourceCode) + '/' + rtrim(fundCode),49),
           sourcecode,
           fundcode,
           sum(amount*-1)
      from receiptDetail 
      where slink in (select slink from dbo.invoiceSLinks(@mainInvoiceId)) 
          and left(slink,1)='t'
      group by sourceCode, fundcode
  end 

  select top 1 @sourceCode = accountCode from dbo.invoiceTotalTF(@mainInvoiceId) where accountType = 'SOURCE'
  select top 1 @fundCode = fundCode from receiptDetail where slink in (select slink from dbo.invoiceSLinks(@mainInvoiceId)) 
          and left(slink,1)='t'

 end

 exec dbo.logit @@procid, 'Begin Transaction... @mainInvoiceId', @mainInvoiceId
 begin transaction
 if @subInvoiceType <> 'A'
 begin

  --run fund accrued receivable account verification

  select @accountBlob = dbo.glAccountGetFundAccrualBlob(@fundcode, 'ACCRUED RECEIVABLE')
  select 
   @accrualcode = dbo.readString('@accrualcode=', @accountBlob),
   @targetcode = dbo.readString('@fundcode=', @accountBlob)

  exec dbo.glAccountVerification @accrualcode, 'ACCRUED RECEIVABLE', 'CREATE', @targetAccountCode=@targetcode
 end  
   insert invoices (typ,postDate,invoiceId,[taxyear],[TaxRollId],[PARCEL],[ITEM],[NAME],[BUSINESSNAME],[STATUS], origin, originId)
    select 
     @subInvoiceType,
     @invoiceDate,
     @mainInvoiceId,
     [taxyear],
     0,
     '' as [PARCEL],
     0.0 as [ITEM],
     '' as [NAME],
     '' as [BUSINESSNAME],
     '' as [STATUS],
     @origin,
     @originId
     from invoices where id=@mainInvoiceId
      
   set @invoiceId=isnull((select top 1 id from invoices where [POSTDATE]=@invoiceDate and invoiceId=@mainInvoiceId order by id desc),0)
   set @slink='t'+cast(@invoiceId as varchar)
   
  if left(@description,4) = 'VOID'
   begin
   insert receiptDetail (slink,description,subDescription,sourceCode,fundCode,amount) 
    select @slink,* from @rd
   end
  else
   begin
   insert receiptDetail (slink,description,subDescription,sourceCode,fundCode,amount) 
    select
     @slink,
     @description,
     rtrim(@sourceCode) + '/' + rtrim(@fundCode),
     @sourceCode,
     @fundcode,
     cast(@amount as varchar(50))
   end

  -- add to the gl stage
  exec dbo.subInvoiceStageGL @invoiceId

 if (select balance from dbo.glSummary(@invoiceId,'t')) <> 0
  begin
   exec dbo.logit @@procid, 'Rollback Transaction... @mainInvoiceId', @mainInvoiceId
   rollback transaction
   return
  end
 else
  
  -- post to the gl
  declare @test int = substring(@slink,2,14)
  exec dbo.glPost @test, @styp, @smode
  
  -- Check glDetail for good data
  if (select dbo.glPostCheck(@slink)) = 1 
  begin
   rollback transaction
   if @smode = 0
    begin
     select '@code=1;@message=Failed post GL check. Contact Support. (txinv.3);'
    end
   return
  end

  exec dbo.logit @@procid, 'Commit Transaction... @mainInvoiceId', @mainInvoiceId
  commit transaction

end