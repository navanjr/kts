create procedure dbo.officialDepositCRUD(
 @mode int,
 @depositId int = 0,
 @deletePosted varchar(10) = 'FALSE',
 @debugMode varchar(5) = 'FALSE',
 @depositDate int = null,
 @linkTrust varchar(5) = 'TRUE',
 @newObjectId int = null output,
 @voidDeposit varchar(5) = 'FALSE'
) as
begin
 
 exec dbo.logit @@procid, '@mode', @mode, '@depositDate', @depositDate, '@linkTrust', @linkTrust

 declare 
  @slink varchar(15) = 'o' + cast(@depositId as varchar),
  @officialId int,
  @officialName varchar(50),
  @accountCode varchar(50),
  @accountDesc varchar(50),
  @fullName varchar(50),
  @station varchar(50),
  @whoami varchar(500) = dbo.whoami(),
  @ticketNo varchar(50) = ''

-- Create Treasurers Official Deposit only!
 if @mode = 0 and @linkTrust = 'TRUE'
 begin

  select top 1
   @depositDate = coalesce(@depositDate,dbo.clarionDate(getdate())),
   @officialId = b.id,
   @officialName = b.key2,
   @accountCode = a.Key1,
   @accountDesc = a.key2
  from Object a
  join Object b on b.A3 = a.Key1 and b.typ = 4601
  where a.TYP = 4701 and a.Key3 = 'TRUST'

  select top 1 @ticketNo = cast(cast(isnull(b2,'0') as money) + 1 as int) 
   from object 
   where typ=4522 
     and a1=@officialName 
     and a17='Posted' 
     and olink1=(select fiscalyear from dbo.fpinfo(@depositDate)) 
   order by cast(b2 as money) desc
 
  set @ticketNo=isnull(@ticketNo,1)

  select 
   @fullName = fullName,
   @station = dbo.readstring('@station=',@whoami)
  from dbo.users where ini = dbo.readstring('@ini=',@whoami)

  if @accountCode is null
  begin
   exec dbo.logit @@procid, 'we are likely missing a code on our Treasurers Official Fund for Trust... Bailing'
   select '@code=1;@message=Missing TRUST code on the Treasurers Official Fund Account;'
   return
  end

  exec dbo.logit @@procid, 'create object record @fullName', @fullName, '@station', @station

  declare @newIdWt newIdWt
  insert object (typ, key2, Key3, link2, a1, a2, b1, b10, a18, b13, b2)
  output inserted.id into @newIdWt
  select 4522, @depositDate, 'OFFICIAL', @officialId, @officialName, @accountCode, @accountDesc, @depositDate, @fullName, @station,  @ticketNo 
  
  select @newObjectId = max(id) from @newIdWt

  exec dbo.officialDepositLinkTrust @newObjectId, 'LINK ALL' 

  return

 end

-- Delete
 if @mode = 3
 begin
-- bail if posted and @deletePosted = 'False'
  if dbo.isPosted(@slink) = 'TRUE' and @deletePosted = 'False'
  begin
   exec dbo.logit @@procid, 'official deposit already Posted... Bailing.'
   return
  end

-- bail if period is locked
  if (select fpStatus from dbo.glStatus(@slink)) = 'locked'
  begin
   exec dbo.logit @@procid, 'Period is locked... Bailing.'
   return
  end

-- bail if linked to a bank deposit
 if exists(select * from paid where (slink = @slink or officialDepositId = @depositId) and isnull(depositId,0)>0)
   begin
    exec dbo.logit @@procid, 'Linked to bank deposit... Bailing. @officialdepositId',@depositId

   return
   end


  declare
   @tokenSlink varchar(15),
   @token int,
   @tokenInt int


  exec dbo.logit @@procid, 'Begin Transaction...'
  begin transaction

--remove official deposit (object) and gl and paid records
   if @voidDeposit <> 'TRUE'
    begin
     update object set typ = -4522 where typ = 4522 and id = dbo.slinkId(@slink)
    end
   else
    begin
     update object set a17 = 'VOID' where typ = 4522 and id = dbo.slinkId(@slink)
    end
   delete paid where slink = @slink
   update paid set officialDepositId = 0 where officialDepositId = @depositId

   delete glDetail where slink = @slink
   delete glDetailStage where slink = @slink

  if @debugMode = 'TRUE'
  begin
   exec dbo.logit @@procid, 'Rollback Transaction...'
   rollback transaction
   return
  end 

  exec dbo.logit @@procid, 'Commit Transaction...'
  commit transaction

 end

 return
end