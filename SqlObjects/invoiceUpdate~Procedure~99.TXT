create procedure dbo.invoiceUpdate(
 @invoiceId int = null,
 @taxyear varchar(4) = null,
 @tallyEcho int = null,
 @recurseProtection varchar(5) = 'FALSE'
) as
-- this proc is used by post routines to keep invoice sub totals accurate
begin
 set nocount on

 -- if @taxyear passed. update all invoices in this taxyear with invoiceDue of Zero
 if isnull(@taxyear,'') > '  0'
 begin
  exec dbo.logit @@procid, 'update all Zero invoices in @taxyear', @taxyear
  declare @tokenId int
  declare @wt table(id int)
  insert @wt select id from invoices where taxyear = @taxyear and invoiceDue = 0
  while exists(select * from @wt)
  begin
   select @tokenId = min(id) from @wt
   exec dbo.invoiceUpdate @tokenId
   delete @wt where id = @tokenId
  end   

 end

 if exists(select * from invoices where id = @invoiceId)
 begin
  declare 
   @amount money = 0,
   @due money = 0,
   @feeDue money = 0,
   @taxRuleDue money = 0,
   @parentInvoiceId int = 0

  select 
   @amount = sum(case when accountType = 'SOURCE' then amount * -1 else 0 end),
   @due = sum(case when accountType = 'RECEIVABLE' then amount else 0 end),
   @feeDue = sum(case when accountType = 'RECEIVABLE' then feeamount else 0 end)
   from dbo.invoiceGLTotalTF(@invoiceId)
   where status='Posted'

  select @parentInvoiceId = statementId from invoices where id = @invoiceId

-- get the linked invoice due if I am a parent
  select
   @taxRuleDue = sum(invoiceDue)
  from invoices where statementId = @invoiceId

  exec dbo.logit @@procid, '@invoiceId', @invoiceId, '@amount', @amount, '@tally', @tallyEcho, '@taxRuleDue', @taxRuleDue --, '@due', @due, '@feeDue', @feeDue

  update invoices set
   invoiceAmount = isnull(@amount,0),
   invoiceDue = isnull(@due,0),
   subInvoiceDue = isnull(@feeDue,0),
   taxRuleDue = isnull(@taxRuleDue,0)
  where id = @invoiceId

-- if i am a child invoice update my parent
 if isnull(@parentInvoiceId,0) > 0 and @recurseProtection = 'FALSE'
  exec dbo.invoiceUpdate @parentInvoiceId, @recurseProtection = 'TRUE'

 end
 else 
 begin

  exec dbo.logit @@procid, 'no invoice to update... @invoiceId', @invoiceId

 end

 return

end