create function [dbo].[unpaidTF](
 @invoiceId int
) returns @rt table(
 invoiceid int,
 origin varchar(50),
 year varchar(4),
 invoiceDue Money,
 interestAsOfDate varchar(50),
 penaltyPercent decimal(19,9),
 penalty Money,
 link2parentInvoice int,
 trcode varchar(50),
 trSection int
)

as
begin

 declare 
  @parcel varchar(50),
  @year varchar(4),
  @typ varchar,
  @name varchar(50),
  @asOf varchar(10),
  @totalTax money

-- get variables from our invoice
 select
  @parcel = parcel,
  @name = name, 
  @year = taxyear,
  @typ = TYP 
 from invoices 
 where ID = @invoiceId

-- get all the other related invoices by parcel or name
 if @typ = 'R'
  insert into @rt (invoiceId,origin,year,invoiceDue,link2ParentInvoice,trcode,trSection)
  select  
   ID,
   'Parcel Search',
   taxyear,
   invoiceDue,
   invoiceId,
   case when ID = @invoiceID then 'Current Yr Tax' else 'Past Due' end,
   1
  from invoices
  where PARCEL = @parcel
   and TYP = 'R'
   and invoiceDue != 0
   and id not in (select invoiceId from @rt)
 
 if @typ = 'P'
  insert into @rt (invoiceId,origin,year,invoiceDue,link2ParentInvoice,trcode,trSection)
  select  
   ID,
   'Name Search',
   taxyear,
   invoiceDue,
   invoiceId,
   case when ID = @invoiceID then 'Current Yr Tax' else 'Past Due' end,
   1
  from invoices
  where name = @name
   and TYP != 'R'
   and invoiceDue != 0
   and id not in (select invoiceId from @rt)

-- get the sub or fee invoices, but do NOT insert fees into the table if there are no fees
  insert into @rt (invoiceId,origin,year,invoiceDue,link2ParentInvoice,trcode,trSection)
 select 
  id,
  'fee search',
  taxyear,
  invoicedue,
  invoiceId,
  'Past Due',
   1
 from invoices 
 where invoiceId in (select invoiceId from @rt)

 set @asof = '01/15/' + cast(@year + 1 as varchar)

 update @rt set
  interestAsOfDate = @asof,
  penaltyPercent = dbo.penaltyPercent(invoiceId,@asof)

 update @rt set
  penalty = invoicedue*(penaltypercent/100)
  
--Need to NOT insert penalities into the table if there are no penalties.  Check for nulls.
  if isnull((select sum(penalty) from @rt),0) != 0 
  begin
    insert @rt (invoiceDue,trcode,trSection) select sum(penalty), 'Past Due',1 from @rt
  end

--Grand Total Section 1
 select @totalTax = sum(invoiceDue) from @rt
 insert @rt (invoiceDue,trcode,trSection)
  select @totalTax, 'Total Tax',1
--Grand Total
 insert @rt (invoiceDue,trcode,trSection)
  select @totalTax, 'Total Tax',2

--Half Tax Section 2
 if isnull((select sum(invoiceDue) from @rt where trcode = 'Current Yr Tax'),0) > 25
  insert @rt (invoiceDue,trcode,trSection)
   select sum(invoiceDue)*.5, 'Half Tax',2 from @rt where trcode = 'Current Yr Tax'
 	
 return
end
