CREATE function dbo.dailyDataCollections(@theDate int, @days int, @ordSeed varchar(50), @filterFlag varchar(10)) returns 
@rt table (
 id int,
 ord varchar(10),
 rowstring varchar(250),
 glBalanceString varchar(50),
 rowstring1 varchar(100),
 sourceNo varchar(50),
 recordCnt varchar(50),
 sourceAmt varchar(50)
)
begin

 declare @wt table(
  id int,
  ord varchar(50),
  description varchar(50),
  subDescription varchar(50),
  minControlNumber varchar(50),
  maxControlNumber varchar(50),
  rowCnt int,
  amount money,
  glBalance money
 )
 declare @wtRaw table(
  id int,
  ord varchar(50),
  description varchar(50),
  subDescription varchar(50),
  receiptNumber varchar(50),
  amount money,
  slink2 varchar(15)
 )
  declare @accounts table(
  accountId int,
  collectionDescription varchar(50),
  reportOrder varchar(50)
 )

-- get the collection accounts 
 insert @accounts select accountId,collectionDescription,reportOrder from dbo.glCollectionAccount

-- get the apportionment account, maybe :)
 if substring(@filterFlag,2,1) = '1'
 insert @accounts 
  select accountId, 'Apportionment Receivable', '02z' 
   from dbo.glAccounts where accountId not in (select accountId from paycodes)
   and accountType in ('RECEIVABLE','FUND') 
   and accountId not in (select accountId from @accounts)

-- insert all raw receipt data
 insert @wtRaw
 select
  b.accountId,
  a.reportOrder,
  a.collectionDescription,
  b.accountDesc,
  c.key1,
  b.amount,
  b.slink
 from @accounts a, glDetail b, object c
 where a.accountId = b.accountId
  and c.typ in (4502) --,4512,4522
  and dbo.slinkType(b.slink) = 'o'
  and substring(b.slink,2,14) = c.id
  and b.date between @theDate and @theDate + @days

-- insert all raw receipt data
 insert @wtRaw
 select
  b.accountId,
  a.reportOrder,
  a.collectionDescription,
  b.accountDesc,
  c.receiptNumber,
  b.amount,
  b.slink
 from @accounts a, glDetail b, receiptLink c
 where a.accountId = b.accountId
  and dbo.slinkType(b.slink) = 'l'
  and substring(b.slink,2,14) = c.id
  and b.date between @theDate and @theDate + @days

/* removed - too slow
-- update receiptNumber for object receipts
 update a set a.receiptNumber = b.key1 
 from @wtRaw a, object b
 where a.slink2 = 'o' + cast(b.id as varchar)

-- update receiptNumber for tax receipts
 update a set a.receiptNumber = b.receiptNumber 
 from @wtRaw a, receiptLink b
 where a.slink2 = 'l' + cast(b.id as varchar)
*/

 insert @wt
 select
  id,
  ord,
  description,
  subDescription,
  min(receiptNumber),
  max(receiptNumber),
  COUNT(receiptNumber),
  abs(SUM(amount)),
  0
 from @wtRaw
 group by
  id,
  ord,
  description,
  subDescription
 order by
  ord

 update a set a.glBalance = (select sum(amount) from glDetail where accountId = a.id) from @wt a

--insert main data
 insert @rt
 select
  cast(case when description = 'Apportionment Receivable' then '8' else '9' end + cast(id as varchar) as int),
  @ordSeed + ord + 'b',
  case when description = 'Apportionment Receivable' 
   then '   ' + dbo.padRight(subDescription,' ',61) + dbo.padLeft('',' ',16)
   else dbo.dailyDataFormatCollections(subDescription,minControlNumber,maxControlNumber,rowCnt,amount)
  end,  
  dbo.padLeft(convert(varchar,glBalance,1),' ',16),
  '   ' + dbo.padRight(subDescription,' ',61),  
  case when description = 'Apportionment Payable' 
   then '   ' 
   else     dbo.padLeft(minControlNumber, 0, 5) + ' - ' + dbo.padLeft(maxControlNumber, 0, 5) 
  end,
  case when description = 'Apportionment Payable' 
   then '   '
   else     dbo.padLeft(cast(rowCnt as varchar), ' ', 4)
  end,
  case when description = 'Apportionment Payable' 
   then '   '
   else     dbo.padLeft(convert(varchar,amount,1),' ',16)
  end

 from @wt


   
  

-- insert subtotals
 insert @rt
 select
  0,
  @ordSeed + ord + 'c',
  ' ' + replicate('-',79),
  replicate('-',16),
  ' ' + replicate('-',79),
  replicate('-',22),
  replicate('-',10),
  replicate('-',16)
 from @wt 
 group by ord,description

 insert @rt
 select
  0,
  @ordSeed + ord + 'd',
  dbo.padRight('      Total ' + description, ' ', 60)
  + dbo.padLeft(cast(sum(rowCnt) as varchar), ' ', 4)
  + dbo.padLeft(convert(varchar,sum(amount),1), ' ', 16),
  '',
  dbo.padRight('      Total ' + description, ' ', 60),
  '',
  dbo.padLeft(cast(sum(rowCnt) as varchar), ' ', 4),
  dbo.padLeft(convert(varchar,sum(amount),1), ' ', 16)
 from @wt 
 group by ord,description

 insert @rt
 select
  0,
  @ordSeed + ord + 'e',
  replicate(' ',80),
  '',
  '',
  '',
  '',
  ''
 from @wt 
 group by ord,description

 -- headers
 insert @rt select 0,@ordSeed+'00a','','','','','',''
 insert @rt select 0,@ordSeed+'00aa','  Collections','','  Collections','','',''
 insert @rt select 0,@ordSeed+'00ab',' ' + replicate('=',79),replicate('=',16),' ' + replicate('=',79),replicate('=',22),replicate('=',10),replicate('=',16)

 if exists(select * from @wt where description = 'Depository')
 begin
  declare @reportSub varchar(50)
  select @reportSub = max(ord) from @wt where description = 'Depository'
  insert @rt select 0,@ordSeed+@reportSub+'a','','','','','',''
  insert @rt select 0,@ordSeed+@reportSub+'aa','  Deposits by Officers','','  Deposits by Officers','','',''
  insert @rt select 0,@ordSeed+@reportSub+'ab',' ' + replicate('=',79),replicate('=',16),' ' + replicate('=',79),replicate('=',22),replicate('=',10),replicate('=',16)
 end

-- insert grand total
 if exists(select * from @wt)
 begin
  insert @rt select 0,@ordSeed+'da',dbo.padLeft(replicate('=',20),' ',80),replicate('=',16),dbo.padLeft(replicate('=',20),' ',80),replicate('=',22),replicate('=',10),replicate('=',16)

  insert @rt
  select
   0,
   @ordSeed + 'db',
   dbo.padLeft('Total Collections  ', ' ', 60)
   + dbo.padLeft(cast(sum(rowCnt) as varchar), ' ', 4)
   + dbo.padLeft(convert(varchar,sum(amount),1), ' ', 16),
   dbo.padLeft(convert(varchar,sum(glBalance),1), ' ', 16),
   dbo.padLeft('Total Collections  ', ' ', 50),
   '',
   dbo.padLeft(cast(sum(rowCnt) as varchar), ' ', 4),
   dbo.padLeft(convert(varchar,sum(amount),1), ' ', 16)
  from @wt 
 end
 return
end