create proc dbo.paymentCancel(
 @id int,
 @method varchar(10) = 'CANCEL',
 @verbose varchar(5) = 'TRUE',
 @areYouSure varchar(5) = 'NO',
 @comments varchar(max) = '',
 @reasonToCancel varchar(50) = null, 
 @cancelExpiredVouchers varchar(5) = null,
 @stopexpires int = 0 
) as 
begin
 set nocount on

-- adding support for many ids
 if @id is null and @cancelExpiredVouchers = 'TRUE'
 begin

  declare
   @ids ids,
   @idToken int,
   @idBlob varchar(max)
  
  select @idBlob = ids from dbo.vouchersExpiredTF()

  exec dbo.logit @@procid, '@cancelExpiredVouchers', @cancelExpiredVouchers, '@method', @method, 'many ids passed... @idBlob', @idBlob

  insert @ids select data from dbo.split(@idBlob,',')

  while exists(select * from @ids)
  begin
   select @idToken = min(id) from @ids
   
   exec dbo.logit @@procid, 'looping... @idToken', @idToken, '@method', @method

   exec dbo.paymentCancel
    @id = @idToken,
    @method = @method,
    @areYouSure = 'YES',
    @comments = @comments,
    @reasonToCancel = @reasonToCancel,
    @cancelExpiredVouchers = 'FALSE',
    @stopexpires = @stopexpires

   delete @ids where id = @idToken
  end

  return
 end

 declare 
  @slink varchar(15) = 'o' + cast(@id as varchar),
  @verbPT varchar(50) = dbo.proper(@method) + 'ed',
  @paymentType varchar(50),
  @log varchar(max),
  @cancelPaymentType varchar(50),
  @redeemingPaymentType varchar(50),
  @paymentDebitBalance money

 select @paymentType = a18 from object where typ = 4771 and id = @id

 exec dbo.logit @@procid, 'Starting Specific... @id', @id, '@paymentType', @paymentType, '@verbose', @verbose, '@areYouSure', @areYouSure

 if isnull(@paymentType,'') < '  0'
 begin
  select @log = '@code=1;@message=paymentType was not found... contact support.'
  goto bail
 end

-- bail if we are already redeemed
 select 
  @redeemingPaymentType = paymentType,
  @paymentDebitBalance = amount
 from dbo.paymentDebitBalance(@id)

 if @paymentDebitBalance = 0
 begin
  select @log = '@code=1;@message=Sorry. This ' + @paymentType + ' has already been redeemed by ' + @redeemingPaymentType + ' and can not be ' + @verbPT + '.'
   + ';@caption=' + dbo.proper(@method) + 'ing a ' + @paymentType + '...;'  
  goto bail
 end

-- bail if we have cleared to a bank statement
 if exists(select * from gldetail where slink=@slink and isnull(bsId,0)>0)
 begin
  select @log = '@code=1;@message=Sorry. This ' + @paymentType + ' has cleared the bank and can not be ' + @verbPT
   + ';@caption=' + dbo.proper(@method) + 'ing a ' + @paymentType + '...;'  
  goto bail
 end

-- bail if we are not posted
 if dbo.isPosted(@slink) != 'TRUE'
 begin
  select @log = '@code=1;@message=Sorry. This ' + @paymentType + ' is not posted and can not be ' + @verbPT
   + ';@caption=' + dbo.proper(@method) + 'ing a ' + @paymentType + '...;'  
  goto bail
 end
/* - this is checking what period the voucher was issued not what period it should be cancelled
-- bail if the period is locked
 if (select fpStatus from dbo.glStatus(@slink)) != 'UNLOCKED'
 begin
  select @log = '@code=1;@message=Sorry. The fiscal period is locked.'
   + ';@caption=' + dbo.proper(@method) + 'ing a ' + @paymentType + '...;'  
  goto bail
 end
*/
-- bail if we are trying to void on the wrong day
 if dbo.settingsF('site.voidOnlyOnPostDate','TRUE') = 'TRUE'
  and (select postdate from dbo.glStatus(@slink)) != dbo.clarionToday()
  and @method = 'VOID'
 begin
  select @log = '@code=1;@message=Sorry. This ' + @paymentType + ' can only be ' + @verbPT + ' on the same day it was posted.'
   + ';@caption=' + dbo.proper(@method) + 'ing a ' + @paymentType + '...;'  
  goto bail
 end

 if @areYouSure != 'YES'
 begin
  select @log = '@code=2;@message=Are you sure you wish to ' + dbo.proper(@method) + ' this '
   + @paymentType + '?'
   + ';@caption=' + dbo.proper(@method) + 'ing a ' + @paymentType + '...;'  
  goto bail
 end

-- canceling 
 if @paymentType in ('WARRANT', 'Official Voucher','Trust Voucher','Miscellaneous') and @method in ('VOID', 'CANCEL')
 begin
  if @paymentType like '%Voucher'
   set @cancelPaymentType = dbo.proper(@method) + ' Voucher'
  if @paymentType like '%Warrant'
   set @cancelPaymentType = dbo.proper(@method) + ' Warrant'
  if @paymentType like '%Miscellaneous'
   set @cancelPaymentType = dbo.proper(@method) + ' Miscellaneous'

  exec dbo.logit @@procid, 'canceling or voiding...(1)  @cancelPaymentType', @cancelPaymentType
  exec dbo.paymentCRUD  @mode = 0, @paymentType = @cancelPaymentType, @id = @id, @comments = @comments, @reasonToCancel = @reasonToCancel, @stopexpires = @stopexpires
  return
 end

-- canceling/voiding cancel payments
-- Deleting glDetail and hiding object records
 if @paymentType in ('Cancel Voucher','Cancel Warrant') and @method = 'VOID'
 begin
  exec dbo.logit @@procid, 'canceling or voiding...(2)  @cancelPaymentType', @cancelPaymentType
  exec dbo.paymentCRUD @mode = 3, @id = @id, @paymentType = @paymentType, @postOverride = 'TRUE'
  return
 end
 if @paymentType in ('Cancel Voucher','Cancel Warrant') and @method = 'CANCEL'
 begin
  exec dbo.logit @@procid, 'canceling or voiding...(3)  @cancelPaymentType', @cancelPaymentType
  exec dbo.paymentCRUD @mode = 3, @id = @id, @paymentType = @paymentType, @postOverride = 'TRUE', @deleteObject = 'TRUE'
  return
 end
 
 select @log = '@code=1;@message=' + dbo.proper(@method) + 'ing of ' + @paymentType + ' is not yet supported, Please Contact Support.;'
   + ';@caption=' + dbo.proper(@method) + 'ing a ' + @paymentType + '...;'  
 goto bail

 return

 bail:
  if @verbose = 'TRUE'
   select @log
  exec dbo.logit @@procid, @log
  return

end