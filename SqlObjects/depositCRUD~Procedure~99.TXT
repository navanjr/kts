create proc [dbo].[depositCRUD]( 
 @mode int,
 @depositId int = null,
 @depositDate int = null,
 @dailyDataBRWId int = null,
 @verbose varchar(5) = 'TRUE',
 @newObjectId int = null output,
 @receiptId int = null
) as
begin
 
 declare 
  @slink varchar(15) = 'o' + cast(@depositId as varchar),
  @fpStatus varchar(10),
  @receiptType varchar(50),
  @bankAccountId int,
  @bankAccountCode varchar(50),
  @bankAccountDesc varchar(50),
  @bankAccountNumber varchar(50),
  @receiptTypeCanidate varchar(50),
  @newIdWt newIdWt,
  @blob varchar(max)

 select
  @fpStatus = fpStatus from glStatus(@slink)

 if charindex('|',isnull(@receiptType,'')) > 0
  set @receiptTypeCanidate = ''
 else 
  set @receiptTypeCanidate = isnull(@receiptType,'')

 exec dbo.logit @@procid, '@mode', @mode, '@depositId', @depositId, '@fpStatus', @fpStatus

-- create a new deposit
 if @mode = 0 and isnull(@dailyDataBRWId,0) < 0
 begin

  set
   @depositDate = coalesce(@depositDate,dbo.clarionDate(getdate()))

  select
   @receiptType = isnull(receiptType,''),
   @bankAccountCode = isnull(depositAccountCode,''),
   @bankAccountDesc = isnull(depositAccountDesc,'')
  from dbo.dailyDataPendingDeposits(@depositDate)
  where id = @dailyDataBRWId

  select
   @bankAccountId = accountId,
   @bankAccountNumber = bankAccount
  from dbo.glaccounts where accountCode = @bankAccountCode

  exec dbo.logit @@procid, '@dailyDataBRWId', @dailyDataBRWId, '@receiptType', @receiptType, '@bankAccountCode', @bankAccountCode, '@bankAccountDesc', @bankAccountDesc
  
-- fork here for Depositing Trust into an Official Deposit
  if @bankAccountCode = 'AAATRUST'
  begin

   exec dbo.officialDepositCRUD @mode = 0,  @depositDate = @depositDate, @linkTrust = 'TRUE', @newObjectId = @newObjectId output

  end
  else
  begin

   select top 1 @newObjectId = id
   from Object
   where typ = 4513
    and A17 != 'Posted'
    and Key3 = @receiptTypeCanidate
    and ID not in (select isnull(depositId,0) from paid)
   order by id

   if isnull(@newObjectId,0) > 0
   begin   

    update object set
     key2 = isnull(@depositDate,''), 
     key3 = @receiptTypeCanidate,
     a1 = @bankAccountDesc,
     a2 = @bankAccountNumber,
     link1 = @bankAccountId
    where typ = 4513 and id = @newObjectId

    exec dbo.logit @@procid, '@existingObjectId', @newObjectId

   end
   else
   begin

-- create object record
    insert object (typ,key2,key3,a1,a2,link1)
    output inserted.id into @newIdWt
    select 4513, isnull(@depositDate,''), @receiptTypeCanidate, @bankAccountDesc, @bankAccountNumber, @bankAccountId

    select @newObjectId = max(id) from @newIdWt
    exec dbo.logit @@procid, '@newObjectId', @newObjectId, '@receiptTypeCanidate', @receiptTypeCanidate

   end

-- link payments maybe
   exec dbo.depositLinkManyPayments @depositId = @newObjectId, @receiptType = @receiptType, @bankAccountCode = @bankAccountCode

  end

-- return the id for futher KTS processing
  if @verbose = 'TRUE'
   select '@code=0;@newObjectId=' + cast(@newObjectId as varchar) + ';'

  return
 end

 if @mode = 0 and isnull(@receiptId,0) > 0
 begin
  
  select @blob = e2 from object where typ=4502 and id=@receiptId
  
  if dbo.readstring('@autoDeposit=',@blob)='TRUE'
  begin
   declare @investmentId int
   select @investmentId = dbo.readstring('@investmentId=',@blob)

   select 
     @bankAccountDesc=a.accountdesc, 
     @bankAccountNumber=a.accountcode, 
     @bankAccountId=a.accountid
   from object i, glaccounts a 
   where i.typ=4706 and i.id=@investmentId
     and a.accountcode=i.a9
  end
  else
  begin
   select 
     @bankAccountDesc='', 
     @bankAccountNumber='', 
     @bankAccountId=0
  end

  set
   @depositDate = coalesce(@depositDate,dbo.clarionDate(getdate()))


-- create object record
    insert object (typ,key2,a1,a2,link1)
    output inserted.id into @newIdWt
    select 4513, isnull(@depositDate,''), @bankAccountDesc, @bankAccountNumber, @bankAccountId

    select @newObjectId = max(id) from @newIdWt
    exec dbo.logit @@procid, '@newObjectId', @newObjectId, '@receiptTypeCanidate', @receiptTypeCanidate

-- return the id for futher KTS processing
  if @verbose = 'TRUE'
   select '@code=0;@newObjectId=' + cast(@newObjectId as varchar) + ';'

  update paid set depositId = @newObjectId where isnull(depositId,0)=0 and slink='o'+cast(@receiptId as varchar)

  return
 end

-- delete a deposit
 if @mode = 3
 begin

-- period Check status
  if @fpStatus = 'locked'
   return

  begin transaction

  delete dbo.glDetailStage where slink = @slink
  delete dbo.glDetail where slink = @slink
  delete object where typ = 4513 and id = @depositId
  update paid set depositId = 0 where depositId = @depositId

  commit transaction
  
 end
 
end
