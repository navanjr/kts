create proc dbo.conversion_mikeFund(
 @mode varchar(50) = 'conversion',
 @method varchar(5) = 'GET',
 @class varchar(50) = null output,
 @code int = 0 output,
 @message varchar(100) = null output,
 @tally int = 0 output,
 @fixProc varchar(100) = null output,
 @guide varchar(100) = null output
) as 
begin
 set nocount on

 if @mode != 'conversion'
  return

 declare @st table(
  FUNDCODE varchar(8),
  NAME varchar(30),
  COL INT,
  WARREG varchar(1),
  MRF2 INT,
  BCODE varchar(8),
  FCODE varchar(8),
  C3 INT,
  APPFLG varchar(1),
  PL varchar(1),
  CITYACCT varchar(1),
  LEDGER varchar(1),
  SCHFND varchar(1),
  INDDEP varchar(1),
  LEDORDER int,
  LEDPAGE int,
  BUDORD INT
 )

 declare
  @mikeTableName varchar(50) = 'FUND',
  @mikePath varchar(max),
  @sql nvarchar(max)

 select
  @mikePath = dbo.settingsF('conversion.mikepath','') + '\' + @mikeTableName + '.dbf',
  @sql = 'select * from openrowset(''VFPOLEDB'',''' + @mikePath + ''';'''';'''',''SELECT * FROM ' + @mikeTableName + ' where !deleted()'')'

 insert @st exec(@sql)

 if @method = 'GET'
 begin  
  select 
   @class = 'conversion',
   @guide = 'GET|SHOW|FIX',
   @code = case when count(*) > 0 then 1 else 0 end,
   @tally = COUNT(*),  
   @message = 'mike_funds not found in KTS'
  from @st
  where fundCode not in (select accountCode from dbo.glAccounts)
 end

 if @method = 'SHOW'
 begin  

  select * 
  from @st
  where fundCode not in (select accountCode from dbo.glAccounts)

 end

 if @method = 'FIX'
 begin  
  declare
   @acctTypeId int,
   @acctTypeCode varchar(50),
   @acctTypeDesc varchar(50),
   @acctTypeRepo varchar(50)
  
  select
   @acctTypeId = id,
   @acctTypeCode = key1,
   @acctTypeDesc = key2,
   @acctTypeRepo = key3
  from object where typ = 4702 and key1 = 'FUND'

  if not isnull(@acctTypeId,0) > 0
  begin
   select @code=1, @message='Sorry you are missing a FUND account type'
   exec dbo.logit @@procid, '@code',@code,'@message',@message
   return
  end
  insert object (typ,key1,key2,a1,a2,link1,a3,olink5)
  select 4701, fundcode, name, @acctTypeCode, @acctTypeDesc, @acctTypeId, @acctTypeRepo, 'mike'
   from @st where fundcode not in (select accountCode from dbo.glAccounts)

  exec dbo.logit @@procid, 'just inserted into object typ 4701 @@rowCount', @@rowCount
  
  return

 end

end



