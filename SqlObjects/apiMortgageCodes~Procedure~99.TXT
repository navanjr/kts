create procedure dbo.apiMortgageCodes(
 @method varchar(50) = 'GET',
 @batchSize int = null,
 @postFile varchar(50) = 'apiPostMortgageCodes.json',
 @scratchFile varchar(1000) = null,
 @debugMode varchar(5) = 'FALSE',
 @result varchar(max) = null output,
 @dropRawFile varchar(5) = 'FALSE'
) as
begin
 set nocount on
 SET DEADLOCK_PRIORITY LOW

 declare
  @resource varchar(50) = 'mortgagecodes',
  @apiService varchar(50),
  @tempPath varchar(50), 
  @postFileFull varchar(100), 
  @apiCode varchar(50),
  @apiwt JSONHierarchy,
  @filter varchar(500),
  @apiResource varchar(50) = '/v2/treasurer/mortgagecodes',
  @dump varchar(max),
  @log varchar(max),
  @tokenId int,
  @apiResultWt JSONHierarchy,
  @theDate datetime,
  @updated int,
  @inserted int,
  @tally int,
  @goofyChars varchar(100) = '13,10,9,167,248,34,39,129'

 exec dbo.keyAcVars 'acapiservice', @method='GET', @silentMode='TRUE', @dump = @apiService output
 exec dbo.logit @@procid, '@method', @method, '@batchSize', @batchSize, '@apiService', @apiService, @override = 'TRUE'

-- if not @apiService = 'ON' 
--  return
 
 select @tempPath = path from dbo.paths() where name = 'temp'
 select top 1 @apiCode = b12 from object where typ = 40 order by id
 set @postFileFull = @tempPath + '\' + @postFile
 if @scratchFile is null
  set @scratchFile = @tempPath + '\' + 'apiResultMortgageCodes.tmp'

 declare @jsonPrep table(
  id int identity(1,1),
  parentId int,
  objectId int,
  name varchar(50),
  value varchar(500),
  type varchar(50)
 )

 declare @wt table(
  parentId int identity(1,1),
  mortgage_code_link int,
  mortgage_code varchar(50),
  mortgage_company varchar(50),
  address1 varchar(50),
  address2 varchar(50),
  city varchar(50),
  state varchar(50),
  postal varchar(50),
  phone varchar(50),
  fax varchar(50),
  contact_name varchar(50),
  email varchar(50),
  provider varchar(50),
  hide_statement_message varchar(50),
  hide_online varchar(50),
  statement_message varchar(250)  
 )

 if @method = 'JOB'
 begin
  select @batchSize = coalesce(@batchSize, batchSize) from dbo.apiControlBRW() where resource = @resource
  exec dbo.apiMortgageCodes @method = 'POST', @batchSize = @batchSize
 end
 
 if @method = 'POST'
 begin

-- create the post data file then call the API
  insert @wt select top (@batchSize)
   id,
   key1,
   key3,
   a1,
   a2,
   a3,
   a4,
   a5,
   a6,
   a7,
   a8,
   a9,
   a17,
   case when c2 = '1' then '1' else '0' end,
   case when c3 = '1' then '1' else '0' end,
   (select top 1 c1 from object where typ = 40)
   from object with (NOLOCK)
   where typ = 4030
    and k7 < dbo.date112(lasteditdate)
   order by key1

-- bail if your done or update batchSize from tally, maybe... 
  select @tally = count(*) from @wt
  if isnull(@tally,0) = 0
  begin
   exec dbo.logit @@procid, 'all records updated... nothing to do. :)', @override = 'TRUE'
   return
  end
 
  if @batchSize != @tally
  begin
   set @batchSize = @tally
   exec dbo.logit @@procid, 'ATTENTION... Changed... @batchSize', @batchSize, @override = 'TRUE'
  end

  insert @jsonPrep select parentId,null,'site_id',@apiCode,'int' from @wt
  insert @jsonPrep select parentId,null,'mortgage_code_link',mortgage_code_link,'int' from @wt
  insert @jsonPrep select parentId,null,'mortgage_code',mortgage_code,'string' from @wt
  insert @jsonPrep select parentId,null,'mortgage_company', dbo.stripChars(mortgage_company,@goofyChars),'string' from @wt
  insert @jsonPrep select parentId,null,'address1', dbo.stripChars(address1,@goofyChars),'string' from @wt
  insert @jsonPrep select parentId,null,'address2', dbo.stripChars(address2,@goofyChars),'string' from @wt
--  assessed_exemption
  insert @jsonPrep select parentId,null,'city', city,'string' from @wt
  insert @jsonPrep select parentId,null,'state', state,'string' from @wt  
  insert @jsonPrep select parentId,null,'postal', postal,'string' from @wt
  insert @jsonPrep select parentId,null,'phone', phone,'string' from @wt
  insert @jsonPrep select parentId,null,'fax', fax,'string' from @wt
  insert @jsonPrep select parentId,null,'contact_name', contact_name,'string' from @wt
  insert @jsonPrep select parentId,null,'email', email,'string' from @wt
  insert @jsonPrep select parentId,null,'provider',provider,'string' from @wt

  insert @jsonPrep select parentId,null,'hide_statement_message', hide_statement_message,'int' from @wt
  insert @jsonPrep select parentId,null,'hide_online', hide_online,'int' from @wt
  insert @jsonPrep select parentId,null,'statement_message', statement_message,'string' from @wt
  
-- insert lowest level object wrapper
  select @tokenId = MAX(id) + 1 from @jsonPrep
  insert @jsonPrep (objectId,parentId,name,value,type) 
  select parentId,@tokenId,null,'','object' from @jsonPrep group by parentId
-- insert array wrapper
  insert @jsonPrep (objectId,name,value,type) 
  select @tokenId,'','','array'

  insert @apiwt select * from @jsonPrep
  select @dump = 'site_id='+@apiCode+'&rows='+dbo.URLEncode(dbo.toJSON(@apiwt))

  exec spOverwriteTextFile @postFileFull, @dump
  
  exec dbo.api
   @method = @method,
   @resource = @apiResource,
   @postFile = @postFile,
   @debugMode = @debugMode,
   @scratchFile = @scratchFile,
   @cmdOutput = @tokenId output,
   @dump = @dump output

  set @result = @dump 

  insert @apiResultWt select * from dbo.parseJSON(@dump)
  select @updated = cast(stringValue as int) from @apiResultWt where name = 'updated' and valueType = 'int'
  select @inserted = cast(stringValue as int) from @apiResultWt where name = 'created' and valueType = 'int'
  if isnull(@updated,0) + isnull(@inserted,0) =  @batchSize
  begin
   set @theDate = getDate()
   update object  set k7 = @theDate where id in (select mortgage_code_link from @wt)
   exec dbo.logit @@procid, 'Success...', @dump, @override = 'TRUE'
  end
  else
  begin
   exec dbo.logit @@procid, 'Failure...', @dump, @override = 'TRUE'
  end

  return
 end

end

