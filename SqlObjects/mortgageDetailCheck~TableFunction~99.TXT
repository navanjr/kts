create function dbo.mortgageDetailCheck(@sourceId int) returns
@rettable table(message varchar(500),
 status varchar(50),
 taxAmount money,
 feeAmount money,
 taxdescription varchar(50),
 taxsourceCode varchar(50),
 taxfundCode varchar(50),
 feedescription varchar(50),
 feesourceCode varchar(50),
 feefundCode varchar(50),
 taxdetailId int,
 feedetailId int,
 taxcontraId int,
 feecontraId int
)
begin
 
 declare
  @status varchar(50),
  @taxAmount money,
  @feeAmount money,
  @taxdescription varchar(50),
  @taxsourceCode varchar(50),
  @taxfundCode varchar(50),
  @feedescription varchar(50),
  @feesourceCode varchar(50),
  @feefundCode varchar(50),
  @taxdetailId int=0,
  @feedetailId int=0,
  @taxcontraId int=0,
  @feecontraId int=0

 select
  @taxdescription = key1,
  @taxsourceCode = key2,
  @taxfundCode = key3,
  @taxcontraId = case when a2 > ' 0' then isnull((select accountId from glAccounts where accountCode=a2),0) else 0 end
 from object where typ = 4504 and a1='MTG' and key1 like '%TAX%'

 select
  @feedescription = key1,
  @feesourceCode = key2,
  @feefundCode = key3,
  @feecontraId = case when a2 > ' 0' then isnull((select accountId from glAccounts where accountCode=a2),0) else 0 end
 from object where typ = 4504 and a1='MTG' and key1 like '%FEE%'

 -- check for tax description
 if len(@taxdescription)<2 or len(@taxsourceCode)<2 or len(@taxfundCode)<2
 begin
  insert @rettable (message)
   select '@code=1;Mortgage Tax Description does not exist or is incorrect.'
  return
 end

 -- check for fee description
 if len(@feedescription)<2 or len(@feesourceCode)<2 or len(@feefundCode)<2
 begin
  insert @rettable (message)
   select '@code=1;Mortgage Certification Fee Description does not exist or is incorrect.'
  return
 end

 -- check for existing mortgage tax detail
 set @taxdetailId = isnull((select id from receiptdetail where slink='o'+cast(@sourceId as varchar(15)) and description=@taxdescription),0)

 -- check for existing mortgage fee detail
 set @feedetailId = isnull((select id from receiptdetail where slink='o'+cast(@sourceId as varchar(15)) and description=@feedescription),0)


 insert @rettable
  select '@code=0;OK',
  @status,
  0.00,
  0.00,
  @taxdescription,
  @taxsourceCode,
  @taxfundCode,
  @feedescription,
  @feesourceCode,
  @feefundCode,
  @taxdetailId,
  @feedetailId,
  @taxcontraId,
  @feecontraId
 return

end