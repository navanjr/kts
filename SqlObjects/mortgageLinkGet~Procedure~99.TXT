create proc dbo.mortgageLinkGet (
 @taxYear varchar(4),
 @siteId varchar(50) = null
) as 
begin
 set nocount on

 if not exists(select * from dbo.sysobjects where id = object_id(N'[dbo].[temp_mortgageLink]') and OBJECTPROPERTY(id, N'IsTable') = 1)
  create table temp_mortgageLink(
   id int identity(1,1),
   company varchar(100), 
   details varchar(max), 
   parcel varchar(100),
   record_id int,
   year varchar(50),
   site_id int,
   createDate varchar(50),
   taxrollId int,
   companyId varchar(50)
  )

  delete from temp_mortgageLink

  declare 
   @typ int = 4,
   @apiwt JSONHierarchy,
   @filter varchar(500),
   @apiHost varchar(50),
   @apiKey varchar(50),
   @apiResource varchar(50),
   @apiFields varchar(max),
   @dump varchar(max),
   @log varchar(max),
   @scratchFile varchar(1000)

  select @scratchFile = path + '\apiMortgageLink.tmp' from dbo.paths() where name = 'temp'
  if @siteId is null
   select @siteId = apiSiteCode from dbo.site
  select @filter = '"site_id=' + @siteId + '&year=' + @taxYear + '"'
   
  exec dbo.api
   @scratchFile = @scratchFile,
   @method = 'get', 
   @resource = '/v2/treasurer/mortgagelink',
   @debugmode='false',
   @filter = @filter,
   @dump = @dump output

  if len(@dump) > 0
  begin

   begin try
    insert into @apiwt
    select * from dbo.parseJSON(@dump) where name is not null and parent_id is not null
   end try
   begin catch
    set @log = error_message()
    exec dbo.logit @@procid, 'Failed Insert in to @apiwt', @log
    select '@code=1;@message=Failed Insert in to @apiwt:' + @log + ';'
    return
   end catch

   declare @token int, @tally int

   while exists(select * from @apiwt)
   begin
    select @token = min(parent_id) from @apiwt
    insert temp_mortgageLink select 
     isnull((select stringValue from @apiwt where parent_id = @token and name = 'company'),''),
     isnull((select stringValue from @apiwt where parent_id = @token and name = 'details'),''),
     isnull((select stringValue from @apiwt where parent_id = @token and name = 'parcel'),''),
     (select stringValue from @apiwt where parent_id = @token and name = 'id'),
     isnull((select stringValue from @apiwt where parent_id = @token and name = 'year'),''),
     isnull((select stringValue from @apiwt where parent_id = @token and name = 'site_id'),''),
     isnull((select stringValue from @apiwt where parent_id = @token and name = 'created_date'),''),
     null,null
    delete @apiwt where parent_id = @token
   end

-- little data cleanup
-- remove hyphens 
   update temp_mortgageLink set parcel = dbo.stripChars(parcel,'45') 

   select @tally = count(*) from temp_mortgageLink
   exec dbo.logit @@procid, 'total rows received from the API', @tally   

 end
end