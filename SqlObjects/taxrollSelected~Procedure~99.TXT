create procedure dbo.taxrollSelectedMtgCode(
 @receiptId int,
 @InvoiceId int,
 @part varchar(10),
 @method varchar(10)
) as 
begin
 

 declare @rd table(receiptId int, invoiceId int, methodRate money)

 exec dbo.logit @@procid, '@receiptId', @receiptId, '@invoiceId', @invoiceId, '@part', @part, '@method', @method

 if lower(@part) = 'full' and lower(@method) = 'reset'
 begin
  delete receiptlink where receiptId = @receiptId
  return
 end

 -- bail if the id is not an invoice
 if not exists(select * from invoices where id = @invoiceId)
  return

 -- bail if the invoiceDue is zero
 if dbo.invoiceTotalAR(@invoiceId) = 0
  return

 declare @methodRate money
 if lower(@part) = 'half'
  select @methodRate = .5
 else
  select @methodRate = 1

 if lower(@method) = 'tax'
 begin
  if exists(select * from receiptlink where receiptId = @receiptId and invoiceId = @invoiceId)
   delete receiptlink where receiptId = @receiptId and invoiceId = @invoiceId
  else
   insert @rd (receiptId, invoiceId, methodRate) select @receiptId, @invoiceId, @methodRate
 end

 if lower(@method) = 'name'
 begin
  insert @rd (receiptId, invoiceId, methodRate) 
  select @receiptId, id, @methodRate from invoices 
  where name = (select name from dbo.invoices where id = @invoiceId)
   and id not in (select invoiceId from receiptlink where receiptId = @receiptId union select invoiceId from @rd where receiptId = @receiptId)
 end

 if lower(@method) = 'parcel'
 begin
  declare @parcel varchar(60)
  select @parcel=parcel from dbo.invoices where id = @invoiceId

  exec dbo.logit @@procid, '@parcel = ', @parcel, @level = 2   

  if @parcel > ' 0'
  begin
   insert @rd (receiptId, invoiceId, methodRate) 
   select @receiptId, id, @methodRate from invoices 
   where parcel = @parcel
    and id not in (select invoiceId from receiptlink where receiptId = @receiptId union select invoiceId from @rd where receiptId = @receiptId)
  end
 end

 if lower(@method) = 'mortcode'
 begin
  declare @mortcode varchar(60)
  select @mortcode=a.treamort from invoices i, adtax a where i.id = @invoiceId and a.id=i.taxrollId

  exec dbo.logit @@procid, '@mortcode = ', @mortcode, @level = 2   

  if @mortcode > ' 0'
  begin
   insert @rd (receiptId, invoiceId, methodRate) 
   select @receiptId, i.id, @methodRate from invoices i, adtax a
   where a.treamort = @mortcode
    and a.id = i.taxrollId
    and i.id not in (select invoiceId from receiptlink where receiptId = @receiptId union select invoiceId from @rd where receiptId = @receiptId)
  end
 end

 if lower(@method) = 'toggle'
 begin
  if exists(select * from receiptlink where receiptId = @receiptId and invoiceId = @invoiceId)
  begin
   update receiptLink set methodRate = case when methodRate = 1 then .5 else 1 end
   where receiptId = @receiptId and invoiceId = @invoiceId   
  end
 end

 update r set methodRate = 1 from @rd r, invoices i where r.invoiceId=i.id and i.invoiceAmount < 25.01
 
 insert receiptLink (receiptId, invoiceId, methodRate) 
   select * from @rd

 delete from receiptLink where receiptId = @receiptId 
   and invoiceId in (select invoiceId from receiptLink where receiptId in (select id from object where typ=4502 and a17<>'Posted'))
 
 delete from receiptLink where receiptId = @receiptId 
   and dbo.invoiceTotalAR(invoiceId)=0

end