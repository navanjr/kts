create procedure dbo.glDetailStageAdd (
 @id int,
 @styp char(1),
 @accountId int,
 @amount money,
 @stageId int = 0,
 @contraId int = 0,
 @comment varchar(50) = '',
 @sourceCode varchar(60) = '',
 @verbose varchar(5) = 'TRUE'
) as 
begin
 declare
  @code int,
  @message varchar(500),
  @slink varchar(15) = @styp + cast(@id as varchar)

 -- bail if amount is zero
 if @amount = 0.00
 begin
  if @verbose = 'TRUE'
   select '@code=1;@message=Can not stage a GL amount of Zero.;'
  exec dbo.logit @@procid, '@code=1;@message=Can not stage a GL amount of Zero.;'
  return
 end

 -- Pre Stage check 
 exec dbo.glPreStageCheck @id, @styp, @code output, @message output
 if @code = 1
 begin
  if @verbose = 'TRUE'
   select '@code='+cast(@code as varchar)+';@message='+@message+';'
  exec dbo.logit @@procid, '@code', @code,'@message', @message
  return
 end

 declare 
  @accountCode varchar(50),
  @accountDesc varchar(50),
  @accountType varchar(60)

 select 
  @accountCode = accountCode,
  @accountDesc = accountDesc,
  @accountType = isnull(accountType,'')  
 from glAccounts
 where accountId = @accountId
 
 -- check for account summary mode
 if @stageId = -1
 begin
  if exists(select * from glDetailStage where slink = @slink and accountId = @accountId)
   select @stageId = id, @amount = @amount + amount 
   from glDetailStage where slink = @slink and accountId = @accountId
  else
   select @stageId = 0
 end

 if @stageId = 0
 begin
  insert gldetailstage (slink,accountId,accountCode,accountDesc,amount,contraId, comment2, sourceCode, accountType)
   select @slink,@accountId,@accountCode,@accountDesc,@amount,@contraId,@comment,@sourceCode,@accountType
 end

 else

 begin 

  if exists(select * from glDetailStage where id = @stageId)
   update gldetailstage set 
    slink = @slink,
    accountId = @accountId,
    accountCode = @accountCode,
    accountDesc = @accountDesc,
    amount = @amount,
    comment2 = @comment,
    sourceCode = @sourceCode,
    contraId = @contraId,
    accountType = isnull(@accountType,'') 
   where id = @stageId
  else
  insert gldetailstage (slink,accountId,accountCode,accountDesc,amount,comment2,sourceCode,contraId,accountType)
   select @slink,@accountId,@accountCode,@accountDesc,@amount,@comment,@sourceCode,@contraId,@accountType
 end

 -- if contraId exists
 if isnull(@contraId,0)>0
 begin
  declare
   @contraStageId int,
   @contraAmount money
  -- get the stage records id if it already exists
  select @contraStageId = id from glDetailStage where slink = @slink and accountId = @contraId
  -- get the value of the transaction
  select @contraAmount = sum(amount)*-1 from glDetailStage where slink = @slink and contraId = @contraId and accountId <> @contraId
  set @contraStageId = ISNULL(@contraStageId,0)
  if @contraAmount <> 0.00
   begin
    exec dbo.glDetailStageAdd @id, @styp, @contraId, @contraAmount, @contraStageId, @verbose = @verbose
   end
  else
   begin
    if isnull(@contraStageId,0)>1
     begin
      delete from gldetailstage where id=@contraStageId
     end
   end
 end

 if @verbose = 'TRUE'
  select '@code=0;@message=Staging of GL Successful;'
 exec dbo.logit @@procid, '@code=0;@message=Staging of GL Successful;'
 return

end