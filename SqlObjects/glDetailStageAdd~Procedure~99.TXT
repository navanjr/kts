create procedure dbo.glDetailStageAdd (
 @id int,
 @styp char(1),
 @accountId int,
 @amount money,
 @stageId int = 0,
 @contraId int = null
) as 
begin
 declare
  @code int,
  @slink varchar(15) = @styp + cast(@id as varchar)

 -- bail if amount is zero
 if @amount = 0.00
  return

 -- check to see if this source instrument is already posted
 exec dbo.glPreStageCheck @id, @styp, @code output
 if @code = 1
  return

 declare 
  @accountCode varchar(50),
  @accountDesc varchar(50)

 select 
  @accountCode = accountCode,
  @accountDesc = accountDesc
 from glAccounts
 where accountId = @accountId
 
 -- check for account summary mode
 if @stageId = -1
 begin
  if exists(select * from glDetailStage where slink = @slink and accountId = @accountId)
   select @stageId = id, @amount = @amount + amount 
   from glDetailStage where slink = @slink and accountId = @accountId
  else
   select @stageId = 0
 end

 if @stageId = 0
 begin
  insert gldetailstage (slink,accountId,accountCode,accountDesc,amount,contraId)
   select @slink,@accountId,@accountCode,@accountDesc,@amount,@contraId
 end

 else

 begin 
  if exists(select * from glDetailStage where id = @stageId)
   update gldetailstage set 
    slink = @slink,
    accountId = @accountId,
    accountCode = @accountCode,
    accountDesc = @accountDesc,
    amount = @amount 
   where id = @stageId
  else
  insert gldetailstage (slink,accountId,accountCode,accountDesc,amount)
   select @slink,@accountId,@accountCode,@accountDesc,@amount
 end

 -- if contraId exists
 if @contraId is not null
 begin
  declare
   @contraStageId int,
   @contraAmount money
  -- get the stage records id if it already exists
  select @contraStageId = id from glDetailStage where slink = @slink and accountId = @contraId
  -- get the value of the transaction
  select @contraAmount = sum(amount)*-1 from glDetailStage where slink = @slink and contraId = @contraId and accountId <> @contraId
  set @contraStageId = ISNULL(@contraStageId,0)
  exec dbo.glDetailStageAdd @id, @styp, @contraId,@contraAmount, @contraStageId
 end

 return

end





