create procedure dbo.glDetailStageAdd (
 @sourceId int,
 @accountId int,
 @amount money,
 @stageId int = 0,
 @contraId int = null
) as 
begin
 -- bail if amount is zero
 if @amount = 0.00
  return

 -- check to see if this source instrument is already posted
 if (select a17 from object where id=@sourceId) = 'Posted'
  return

 declare 
  @accountCode varchar(50),
  @accountDesc varchar(50)

 select 
  @accountCode = key1,
  @accountDesc = key2
 from object
 where typ = 4701 and id = @accountId
 
 -- check for account summary mode
 if @stageId = -1
 begin
  if exists(select * from glDetailStage where sourceId = @sourceId and accountId = @accountId)
   select @stageId = id, @amount = @amount + amount 
   from glDetailStage where sourceId = @sourceId and accountId = @accountId
  else
   select @stageId = 0
 end

 if @stageId = 0
 begin
  insert gldetailstage (sourceId,accountId,accountCode,accountDesc,amount,contraId)
   select @sourceId,@accountId,@accountCode,@accountDesc,@amount,@contraId
 end

 else

 begin 
  if exists(select * from glDetailStage where id = @stageId)
   update gldetailstage set 
    sourceId = @sourceId,
    accountId = @accountId,
    accountCode = @accountCode,
    accountDesc = @accountDesc,
    amount = @amount 
   where id = @stageId
  else
  insert gldetailstage (sourceId,accountId,accountCode,accountDesc,amount)
   select @sourceId,@accountId,@accountCode,@accountDesc,@amount
 end

 -- if contraId exists
 if @contraId is not null
 begin
  declare
   @contraStageId int,
   @contraAmount money
  -- get the stage records id if it already exists
  select @contraStageId = id from glDetailStage where sourceId = @sourceId and accountId = @contraId
  -- get the value of the transaction
  select @contraAmount = sum(amount)*-1 from glDetailStage where sourceId = @sourceId and contraId = @contraId and accountId <> @contraId
  set @contraStageId = ISNULL(@contraStageId,0)
  exec dbo.glDetailStageAdd @sourceId, @contraId,@contraAmount, @contraStageId
 end

 return

end




