create proc dbo.collectionsDocProcessing(
 @taxrollDetailId int,
 @documentId int,
 @method varchar(50),
 @date int = null,
 @days int = null,
 @order varchar(50) = 'Parcel'
) as
begin

 exec dbo.logit @@procId, '@method', @method, '@taxrollDetailId', @taxrollDetailId, '@documentId', @documentId

 declare
  @dtype varchar(50),
  @keyCode varchar(50),
  @ordToken varchar(250),
  @documentName varchar(50),
  @tally int

 select
  @dtype = dtype,
  @keyCode = keyCode
 from dbo.collectionsItem(@taxrollDetailId)

 select
  @documentName = documentName
 from dbo.documents where documentId = @documentId
 
 if @method = 'print'
 begin

  if not exists(select * from  dbo.collectionsDetailBRW(@taxrollDetailId,@date,@days) where documentName = @documentName and date between @date and @date + @days and charindex('Add fee',display)=0)
   insert taxrollDetail (parcelNumber,itemNumber,dtype,comments,documentName)
   select
    a.parcel,
    a.item,
    'print',
    'Printed ' + @documentName,
    @documentName
   from dbo.collectionsItem(@taxrollDetailId) a
 
  return
 end

 if @method = 'printThru'
 begin

  select @ordToken = case when @order = 'Parcel' then ordB else ord end from dbo.collectionsBRW(@dtype, @documentId, null, null,@taxrollDetailId)

  exec dbo.logit @@procId, '@ordToken', @ordToken, '@documentName', @documentName, '@taxrollDetailId', @taxrollDetailId

  insert taxrollDetail (parcelNumber,itemNumber,dtype,comments,documentName)
  select
   isnull(parcel,''),
   isnull(item,0.0),
   'print',
   'Printed ' + @documentName,
   @documentName
  from dbo.collectionsBRW(@dtype, @documentId, null, null,0) 
  where case when @order = 'Parcel' then ordB else ord end <= @ordToken
   and keyCode not in (select keyCode from dbo.collectionsPrintedTF(@dtype, @documentId, @date, @days))

  return
 end

 if @method = 'resetOne'
 begin
   delete taxrollDetail
   where dtype = 'print'
    and documentName = @documentName
    and dbo.clarionDate(documentDateTime) between @date and @date + @days
    and dbo.collectionsKeyCode(@dtype,parcelNumber,itemNumber) = @keyCode
 
   exec dbo.logit @@procId, 'just Deleted... @@rowCount', @@rowCount
 end

 if @method in ('resetStage','resetThru')
 begin

  select @ordToken = case when @order = 'Parcel' then ordB else ord end from dbo.collectionsBRW(@dtype, @documentId, null, null,@taxrollDetailId)
  select @tally = count(*) from dbo.collectionsBRW(@dtype, @documentId, null, null,0) where case when @order = 'Parcel' then ordB else ord end >= @ordToken

  exec dbo.logit @@procId, '@ordToken', @ordToken, '@documentName', @documentName

  if @method = 'resetStage'
  begin
   select '@ordToken=' + @ordToken + ';@tally=' + cast(@tally as varchar) + ';'
   return
  end
  if @method = 'resetThru'
  begin
   delete taxrollDetail
   where dtype = 'print'
    and documentName = @documentName
    and dbo.clarionDate(documentDateTime) between @date and @date + @days
    and dbo.collectionsKeyCode(@dtype,parcelNumber,itemNumber) in (select keyCode from dbo.collectionsBRW(@dtype, @documentId, null, null,0) where case when @order = 'Parcel' then ordB else ord end >= @ordToken)
 
   exec dbo.logit @@procId, 'just Deleted... @@rowCount', @@rowCount
   
   return
  end
 end

end