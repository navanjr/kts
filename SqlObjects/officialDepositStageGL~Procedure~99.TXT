create procedure dbo.officialDepositStageGL(
 @officialDepositId int,
 @debug varchar(5) = 'FALSE'
) as
begin

 set nocount on
 
 declare @stage table(
  accountId int,
  accountCode varchar(50),
  accountDesc varchar(50),
  amount money
 )
 
 declare @styp char(1) = 'o'
 declare
  @slink varchar(15) = @styp + cast(@officialDepositId as varchar),
  @code int,
  @contraId int,
  @officialFundAccountId int,
  @tokenId int,
  @tokenAmount money,
  @posName varchar(50) = dbo.posName(@officialDepositId)  --dbo.readstring('@posName=',dbo.whoami())
 
 select 
  @code = code,
  @contraId = contraId ,
  @officialFundAccountId = arAccountId
 from dbo.officialDepositCheck(@officialDepositId,null)

 exec dbo.logit @@procid,
  '@officialDepositId', @officialDepositId, 'check results - @code', @code, '@contraId', @contraId, '@officialFundAccountId', @officialFundAccountId

-- Credit Suspense Accounts from any linked Trust Receipts
 insert @stage
 select
  a.accountId, a.accountCode, a.accountDesc, gld.amount * -1
 from paid p
  join gldetail gld on gld.slink2 = 'p'+cast(p.id as varchar)
  join dbo.glAccounts a on a.accountCode = gld.accountCode and a.accountType = 'SUSPENSE'
 where p.officialDepositId = @officialDepositId

-- Debit Suspense Accounts from any linked Trust Receipts
 insert @stage (accountCode, amount)
 select
  @posName + '.OFFICIAL.' + dbo.splitF(a.accountCode,'.',3), gld.amount
 from paid p
  join gldetail gld on gld.slink2 = 'p'+cast(p.id as varchar)
  join dbo.glAccounts a on a.accountCode = gld.accountCode and a.accountType = 'SUSPENSE'
 where p.officialDepositId = @officialDepositId

-- Debit Suspense Accounts for any official Deposit Items
 insert @stage (accountCode, amount)
 select
  @posName + '.OFFICIAL.' + p.paycode, p.amount
 from paid p
 where p.slink = @slink

-- Credit to the Official Fund Account
 insert @stage 
 select accountId, accountCode, accountDesc, (select sum(amount) * -1 from @stage)
 from dbo.glAccounts where accountId = @officialFundAccountId

 update a set
  a.accountId = b.accountId,
  a.accountDesc = b.accountDesc
 from @stage a
 join dbo.glAccounts b on a.accountCode = b.accountCode 

 if @debug = 'TRUE'
 begin
  select * from @stage
  return
 end

-- remove any staged GL records
 delete glDetailStage where slink = @slink 

 insert gldetailstage (slink, accountId, accountCode, accountDesc, amount, contraId)
 select @slink, accountId, accountCode, accountDesc, sum(amount), @contraId
 from @stage where amount != 0
 group by accountId, accountCode, accountDesc

end