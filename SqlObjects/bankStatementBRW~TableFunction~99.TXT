create function dbo.bankStatementBRW(
 @id int,
 @accountId int,
 @showFlag int, 
 @warrantFlag int 
) returns @rt table(
 id int,
 fpid int,
 slink varchar(15),
 description varchar(100),
 subDescription varchar(100),
 dbAmount money,
 crAmount money,
 selectedFlag char(1),
 ord varchar(50)
)
begin

 declare @showTable table(id int)
 insert @showTable select 0
 if @showFlag = 1
  insert @showTable select @id

-- get all the gldetail for this account that is not cleared and maybe include the items cleared on this statement
 insert @rt (id,fpid,slink,dbAmount,crAmount,subDescription,selectedFlag)
 select id* -1, fpid, slink,
  case when amount > 0 then amount else 0 end,
  case when amount < 0 then amount else 0 end,
  slink,
  case when isnull(bsId,0) = 0 then '' else 'X' end
 from dbo.glDetail 
 where accountId = @accountId
  and isnull(bsId,0) in (select id from @showTable)

-- populate the items with more details
 update a set
  a.description = '   ' + dbo.date1(b.key2),
  a.subDescription = c.template + ' #' + case when b.typ=4771 then b.key3 + ', '+b.a18+' #' + b.key1 else b.key1 end,
  ord = cast(fpid as varchar) + 'b' 
 from @rt a, object b, template c
 where left(slink,1) = 'o' 
  and cast(substring(slink,2,14) as int) = b.id
  and b.typ = c.id

-- add Period headers for the account details
 insert @rt (id,description,ord)
  select 0,'Period: ' + b.key1 + ' ' + key2,b.id 
   from @rt a, object b
   where a.fpid = b.id
   group by b.key1,b.key2,b.id

-- add Warrants, Maybe
 if not @warrantFlag = 1
  return
 
 declare @limitfundcode varchar(50)
 
 select @limitfundcode = b.accountCode from object a, glAccounts b where a.id = @id and b.targetaccountCode = a.a16 and b.accountType = 'WARRANT' and a.a16>' 0'

 insert @rt (id,description,ord) select 0,'','w' 
 insert @rt (id,description,ord) select 0,'Warrants: ','ww' 
 insert @rt (id, description, subDescription, dbAmount, ord)
  select b.id,
   '   ' + dbo.date1(b.key2) + ' - #' + b.key3,
   b.a2,
   sum(a.amount),
   'ww' + a.accountCode + b.key3 
  from dbo.glDetail a, object b
  where a.slink = 'o' + cast(b.id as varchar)
   and a.accountId in (select accountId from glAccounts where accountType = 'WARRANT' and (isnull(@limitfundcode,'ALL')='ALL' or accountCode=@limitfundcode))
   and b.link1 = 0
  group by a.accountCode, a.accountDesc, b.key2, b.a2, b.key3, b.id
  having sum(amount) > 0

-- add header rows for fund
 insert @rt (id, description, subDescription, ord)
  select 0,
   ' -- ' + a.accountCode + ' - ' + isnull((select d.accountDesc from glAccounts c, glAccounts d where d.accountCode=c.targetAccountCode and c.accountId=a.accountId),''),
   '',
   'ww' + a.accountCode
  from dbo.glDetail a, object b
  where a.slink = 'o' + cast(b.id as varchar)
   and a.accountId in (select accountId from glAccounts where accountType = 'WARRANT' and (isnull(@limitfundcode,'ALL')='ALL' or accountCode=@limitfundcode))
   and b.link1 = 0
  group by a.accountCode, a.accountDesc, a.accountId
 
 return
end