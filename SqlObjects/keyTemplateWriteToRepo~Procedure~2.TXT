create procedure dbo.keyTemplateWriteToRepo( @id int )
as
begin
 set nocount on

 declare
  @gitCommitter varchar(10),
  @txt varchar(max),
  @path varchar(100),
  @fullPath varchar(1000),
  @tally int,
  @idToken int,
  @newNamingConvention int = 1

 select @gitCommitter = dbo.settingsF('git.gitcommitter', '')

-- bail if we are not a git committer
 if not isnull(@gitCommitter,'FALSE') = 'TRUE'
 begin
  exec dbo.logit @@procid, 'not a git Committer'
  return
 end

 select @path = dbo.settingsF('git.gitpath','')

 declare @wt table(
  id int,
  template varchar(50),
  fullPath varchar(1000),
  data varchar(max),
  processFlag int
 )

 if isnull(@id,0) = 0
  insert @wt select
   id,
   replace(template,'/','..'),
   '',
   cast(options as varchar(max)),
   0
   from template where id > 0 and template > '  0'
 else
  insert @wt select
   id,
   replace(template,'/','..'),
   '',
   cast(options as varchar(max)),
   0
   from template where id = @id and template > '  0'

  update @wt set fullPath = 
  case when @newNamingConvention = 0 then
   @path + '\templates\T_' + cast(id as varchar) + '~' + ltrim(rtrim(template)) + '.TXT'
  else
   @path + '\templates\' + ltrim(rtrim(template)) + '~' + cast(id as varchar) + '.TXT'
  end


 if not exists(select * from @wt)
 begin
  exec dbo.logit @@procid, 'No Templates to Write to Repo'
  return
 end

 select @tally = count(*) from @wt
 if isnull(@tally,0) > 1
  exec dbo.logit @@procid, 'Processing Templates... @tally', @tally
 
 while exists(select * from @wt where processFlag = 0)
 begin
  select top 1
   @idToken = id,
   @fullPath = fullPath,
   @txt = data
  from @wt where processFlag = 0 
  order by template 
 
  exec dbo.spOverwriteTextFile @fileName = @fullPath, @text = @txt
  update @wt set processFlag = 1 where id = @idToken
 end

end