CREATE function dbo.dailyDataDeposits(@theDate int, @days int, @ordSeed varchar(50), @filterFlag varchar(10)) returns 
@rt table (
 id int,
 ord varchar(10),
 rowstring varchar(250),
 glBalanceString varchar(50),
 rowstring1 varchar(100),
 sourceNo varchar(50),
 recordCnt varchar(50),
 sourceAmt varchar(50),
 rowType char(10)
)
begin

 declare @wt table(
  id int,
  ord varchar(50),
  description varchar(50),
  subDescription varchar(50),
  minControlNumber varchar(50),
  maxControlNumber varchar(50),
  rowCnt int,
  amount money,
  glBalance money
 )
 declare @wtRaw table(
  id int,
  ord varchar(50),
  description varchar(50),
  subDescription varchar(50),
  depositNumber varchar(50),
  amount money,
  slink2 varchar(15)
 )
  declare @accounts table(
  accountId int,
  collectionDescription varchar(50),
  reportOrder varchar(50)
 )

-- get the bank accounts 
 insert @accounts select accountId,collectionDescription,reportOrder from dbo.glBankAccounts

-- insert all raw receipt data
 insert @wtRaw
 select
  b.accountId,
  a.reportOrder,
  b.accountCode,
  b.accountDesc,
  c.key1,
  b.amount,
  b.slink
 from @accounts a, glDetail b, object c
 where a.accountId = b.accountId
  and c.typ in (4513)
  and dbo.slinkType(b.slink) = 'o'
  and dbo.slinkId(b.slink) = c.id
  and b.date between @theDate and @theDate + @days

 insert @wt
 select
  id,
  ord,
  description,
  subDescription,
  min(depositNumber),
  max(depositNumber),
  COUNT(depositNumber),
  abs(SUM(amount)),
  0
 from @wtRaw
 group by
  id,
  ord,
  description,
  subDescription
 order by
  ord

 update a set a.glBalance = (select sum(amount) from glDetail where accountId = a.id) from @wt a

--insert main data
 insert @rt
 select
  cast('9' + cast(id as varchar) as int),
  @ordSeed + ord + 'b',
  dbo.dailyDataFormatCollections(subDescription,minControlNumber,maxControlNumber,rowCnt,amount),
  dbo.padLeft(convert(varchar,glBalance,1),' ',16),
  '   ' + dbo.padRight(subDescription,' ',61),  
  dbo.padLeft(minControlNumber, 0, 5) + ' - ' + dbo.padLeft(maxControlNumber, 0, 5),
  dbo.padLeft(cast(rowCnt as varchar), ' ', 4),
  dbo.padLeft(convert(varchar,amount,1),' ',16),
  'detail'
 from @wt

-- insert subtotals
 insert @rt
 select
  0,
  @ordSeed + ord + 'c',
  ' ' + replicate('-',79),
  replicate('-',16),
  ' ' + replicate('-',79),
  replicate('-',22),
  replicate('-',10),
  replicate('-',16),
  'line'
 from @wt 
 group by ord,description

 insert @rt
 select
  0,
  @ordSeed + ord + 'd',
  dbo.padRight('      Total ' + description, ' ', 60)
  + dbo.padLeft(cast(sum(rowCnt) as varchar), ' ', 4)
  + dbo.padLeft(convert(varchar,sum(amount),1), ' ', 16),
  '',
  dbo.padRight('      Total ' + description, ' ', 60),
  '',
  dbo.padLeft(cast(sum(rowCnt) as varchar), ' ', 4),
  dbo.padLeft(convert(varchar,sum(amount),1), ' ', 16),
  'gFooter'
 from @wt 
 group by ord,description

 insert @rt
 select
  0,
  @ordSeed + ord + 'e',
  replicate(' ',80),
  '',
  '',
  '',
  '',
  '',
  'lineFeed'
 from @wt 
 group by ord,description

 -- headers
 insert @rt select 0,@ordSeed+'00a','','','','','','','lineFeed'
 insert @rt select 0,@ordSeed+'00aa','  Deposits','','  Deposits','','','','sHeader'
 insert @rt select 0,@ordSeed+'00ab',' ' + replicate('=',79),replicate('=',16),' ' + replicate('=',79),replicate('=',22),replicate('=',10),replicate('=',16),'line'

 if exists(select * from @wt where description = 'Depository')
 begin
  declare @reportSub varchar(50)
  select @reportSub = max(ord) from @wt where description = 'Depository'
  insert @rt select 0,@ordSeed+@reportSub+'a','','','','','','','lineFeed'
  insert @rt select 0,@ordSeed+@reportSub+'aa','  Deposits by Officers','','  Deposits by Officers','','','','sHeader'
  insert @rt select 0,@ordSeed+@reportSub+'ab',' ' + replicate('=',79),replicate('=',16),' ' + replicate('=',79),replicate('=',22),replicate('=',10),replicate('=',16),'line'
 end

-- insert grand total
 if exists(select * from @wt)
 begin
  insert @rt select 0,@ordSeed+'da',dbo.padLeft(replicate('=',20),' ',80),replicate('=',16),dbo.padLeft(replicate('=',20),' ',80),replicate('=',22),replicate('=',10),replicate('=',16),'line'

  insert @rt
  select
   0,
   @ordSeed + 'db',
   dbo.padLeft('Total Deposits  ', ' ', 60)
   + dbo.padLeft(cast(sum(rowCnt) as varchar), ' ', 4)
   + dbo.padLeft(convert(varchar,sum(amount),1), ' ', 16),
   dbo.padLeft(convert(varchar,sum(glBalance),1), ' ', 16),
   dbo.padLeft('Total Deposits  ', ' ', 50),
   '',
   dbo.padLeft(cast(sum(rowCnt) as varchar), ' ', 4),
   dbo.padLeft(convert(varchar,sum(amount),1), ' ', 16),
   'grandTotal'
  from @wt 
 end
 return
end