create PROCEDURE dbo.spWriteStringToFile(
 @String Varchar(max), --8000 in SQL Server 2000
 @Path VARCHAR(255),
 @Filename VARCHAR(100)
) as
begin

 declare
  @objFileSystem int,
  @objTextStream int,
  @objErrorObject int,
  @strErrorMessage Varchar(1000),
  @Command varchar(1000),
  @hr int,
  @fileAndPath varchar(80)

 if right(@path, 1) = '\'
  set @fileAndPath = @path + @filename
 else
  set @fileAndPath = @path + '\' + @filename

 set nocount on

 select @strErrorMessage='opening the File System Object'
 exec @hr = sp_OACreate  'Scripting.FileSystemObject' , @objFileSystem OUT

 if @HR=0 Select @objErrorObject=@objFileSystem , @strErrorMessage='Creating file "'+@FileAndPath+'"'
 if @HR=0 execute @hr = sp_OAMethod   @objFileSystem   , 'OpenTextFile', @objTextStream OUT, @FileAndPath,8,true

 if @HR=0 Select @objErrorObject=@objTextStream,	@strErrorMessage='writing to the file "'+@FileAndPath+'"'
 set @String = @String + CHAR(10) + CHAR(13)
 if @HR=0 execute @hr = sp_OAMethod  @objTextStream, 'Write', Null, @String

 if @HR=0 Select @objErrorObject=@objTextStream, @strErrorMessage='closing the file "'+@FileAndPath+'"'
 if @HR=0 execute @hr = sp_OAMethod  @objTextStream, 'Close'

 if @hr<>0
	begin
	Declare 
		@Source varchar(255),
		@Description Varchar(255),
		@Helpfile Varchar(255),
		@HelpID int
	
	EXECUTE sp_OAGetErrorInfo  @objErrorObject, 
		@source output,@Description output,@Helpfile output,@HelpID output
	Select @strErrorMessage='Error whilst '
			+coalesce(@strErrorMessage,'doing something')
			+', '+coalesce(@Description,'')
	raiserror (@strErrorMessage,16,1)
	end

 exec sp_OADestroy @objTextStream
 exec sp_OADestroy @objFileSystem
end