create function dbo.invoiceRelated(
 @inquireInvoiceId int
) returns @rt table(
 invoiceId int,
 origin varchar(50)
) as
begin
 
/*
 so the goal is to return
 invoices that are related by name, parcel, owner number and...
 the same items paid together last year
*/
 if isnull(@inquireInvoiceId,0) = 0
   return

 declare
  @item numeric(7,1),
  @name varchar(50),
  @parcel varchar(50),
  @ownernumber numeric(14,2),
  @receiptIds ids,
  @invoiceIds ids

 declare @items table(item varchar(50))

-- start with the invoice you are looking at
 select
  @item = invoices.ITEM,
  @name = invoices.NAME,
  @parcel = invoices.PARCEL,
  @ownernumber = adtax.ownernumber
 from invoices with (NOLOCK) 
  left join adtax with (NOLOCK) on invoices.taxrollid = adtax.id
  where invoices.ID = @inquireInvoiceId


 if @name > '  0'
  insert @rt select id, 'Name' from invoices with (NOLOCK) where name = @name and id != @inquireInvoiceId
 if @parcel > '  0'
  begin
   insert @rt select id, 'Parcel' from invoices with (NOLOCK) where parcel = @parcel and id != @inquireInvoiceId 
    and id not in (select invoiceId from @rt)
   insert @rt select invoiceLinkId, 'Resale' from taxrolldetail with (NOLOCK) where parcelNumber = @parcel 
    and invoiceLinkId != @inquireInvoiceId 
    and invoiceLinkId not in (select invoiceId from @rt)
  end

 insert @rt select invoiceLinkId, 'TaxWarrant' from taxrolldetail with (NOLOCK) where itemNumber = @item 
  and invoiceLinkId != @inquireInvoiceId 
  and invoiceLinkId not in (select invoiceId from @rt)


 if isnull(@ownernumber,0.0)>1.0
  insert @rt 
   select i.id, 'Owner Number' from dbo.invoices i with (NOLOCK), adtax a with (NOLOCK) 
    where i.taxrollid=a.id
     and a.ownernumber = @ownernumber
     and i.id != @inquireInvoiceId
     and i.id not in (select invoiceId from @rt)

 if dbo.settingsF('site.invoiceRelatedIncludeReceiptHistory','TRUE') = 'TRUE' and @ITEM > 0.1
 begin

-- get ALL the receipts where this item has been paid
  insert @receiptIds
  select distinct receiptId from receiptlink with (NOLOCK) where invoiceId in (select ID from invoices with (NOLOCK) where ITEM = @item) 
    and receiptId not in (select id from object with (NOLOCK) where abs(typ)=4003)

-- get all the invoices from these receipts
  insert @invoiceIds
  select distinct invoiceId from receiptlink with (NOLOCK) where receiptId in (select id from @receiptIds) and isnull(ignoreLink,0) = 0

-- get all the item numbers from these invoices
  insert @items select item from invoices with (NOLOCK) where id in (select id from @invoiceIds)

  insert @rt
  select distinct id, 'Receipts'
  from invoices with (NOLOCK)
  where id != @inquireInvoiceId
   and item in (select item from @items)
   and invoiceId = 0
   and id not in (select invoiceId from @rt)
   and taxyear > 2006

 end

 return
end