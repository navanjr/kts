CREATE function [dbo].[taxrollCheck](@id int) returns
@rettable table(
 code int,
 message varchar(500),
 taxyear varchar(4),
 district varchar(15),
 rateName varchar(15),
 amount money,
 fpid int,
 debitId int,
 status varchar(50)
)

begin
 declare @styp char(1) = 't'
 
 declare
  @slink varchar(15) = @styp + cast(@id as varchar),
  @cdate int,
  @fpid int,
  @status varchar(50),
  @taxyear varchar(4),
  @district varchar(15),
  @rateName varchar(15),
  @amount money,
  @debitCode varchar(50),
  @debitId int

  set @fpid=0
 
  begin 
   -- TODO: allow for an "as of" date for importing and posting of tax rolls
   select 
    @taxyear = [REALTAXYEAR],
    @district = case when [SCHOOLDISTRICTMAIN] > ' 0' then [SCHOOLDISTRICTMAIN] else [ORGSCHOOLDISTRICTMAIN] end,
    @rateName = case when [SCHOOLDISTRICTTAXRATE] > ' 0' then [SCHOOLDISTRICTTAXRATE] else [ORGSCHOOLDISTRICTTAXRATE] end,
    @amount = case when [TOTALDUE]>0.00 then [TOTALDUE] else [ORIGINALTOTALDUE] end,
    @cdate = dbo.clariondate(getdate()),
    @status = ''
   from adtax where id = @id
  end 

 -- check for status of Posted
 if exists(select * from glDetail where slink = @slink)
 begin
  insert @rettable 
   select 1,'Tax item has already been posted.',@taxyear,@district,@rateName,@amount,@fpid,@debitId,@status
  return
 end

 -- check for open Fiscal Period
 select @fpid = dbo.glGetFiscalPeriodId(@cdate) 
 if @fpid = 0
 begin
  insert @rettable 
   select 1,'Fiscal Period is locked or does not exist.',@taxyear,@district,@rateName,@amount,@fpid,@debitId,@status
  return
 end

 -- check for valid tax Year
 if @taxYear < '  0'
 begin
  insert @rettable 
   select 1,'Tax Year invalid',@taxyear,@district,@rateName,@amount,@fpid,@debitId,@status
  return
 end

 -- check for debit account
 set @debitCode = @taxYear+'_ADVALOREM_AR'
 select @debitId = accountId from glAccounts where accountCode = @debitCode and accountType = 'RECEIVABLE'
 if isnull(@debitId,0) = 0
 begin
  insert @rettable 
   select 1,'Missing Ad Valorem AR Account. Contact Support. (txchk.1)',@taxyear,@district,@rateName,@amount,@fpid,@debitId,@status
  return
 end
 
 insert @rettable
  select 0,'OK',@taxyear,@district,@rateName,@amount,@fpid,@debitId,@status
 return

end



