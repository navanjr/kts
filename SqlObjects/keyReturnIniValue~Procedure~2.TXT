create procedure dbo.keyReturnIniValue(
 @fileName nvarchar(1000),
 @section varchar(50),
 @Field varchar(50),
 @value varchar(1000) output
) as
begin

 set nocount on

 declare @wt table(
  id int,
  section varchar(50),
  fieldName varchar(50),
  fieldValue varchar(1000),
  data varchar(max)
 ) 

 declare
  @data varchar(max),
  @sectionToken varchar(50),
  @idToken int,
  @sql nvarchar(max),
  @params nvarchar(max)

-- get the raw data
 select
  @sql = 'select @text_file=(select * from openrowset (bulk ''' + @fileName + ''', SINGLE_CLOB) as X)',
  @params = '@text_file varchar(max) OUTPUT'  
 exec sp_executesql
  @stmt = @sql,
  @params = @params,
  @text_file = @data OUTPUT 

 insert @wt (id,data)
 select * from dbo.split(@data, CHAR(10)) where data > '  0'

 update @wt set section = replace(replace(replace(data,'[',''),']',''),char(13),'') where CHARINDEX('[',data) > 0 and CHARINDEX(']',data) > 0

 update a set
  a.section = (select top 1 section from @wt where id < a.id and section is not null order by id desc),
  a.fieldName = (select replace(data,char(13),'') from dbo.split(data,'=') where id = 1),
  a.fieldValue = (select replace(data,char(13),'') from dbo.split(data,'=') where id = 2) 
 from @wt a where section is null

 update @wt set fieldName = replace(replace(fieldName,char(13),''),char(10),'')
 
 select top 1 @value = fieldValue from @wt where section = @section and fieldName = @field

 declare @logBlob varchar(max) = 
  '@section=' + isnull(@section,'null') + ';'
  + '@field=' + isnull(@field,'null') + ';'
  + '@value=' + isnull(@value,'null') + ';'
 
-- exec dbo.logit @@procid, @logBlob
 
 return
end